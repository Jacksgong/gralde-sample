apply plugin: 'maven-publish'

// https://docs.gradle.org/current/userguide/publishing_maven.html#sec:publishing_multiple_modules_to_maven
// publish to the remote Nexus build with `./gradlew clean build publish`

// Sample way to checkout:
// Just execute `gradle clean build publishToMavenLocal`. Check out result stuff on the
// `~/.m2/repository/cn/dreamtobe/splitaar/sample/splitaar-sample-api` and
// `~/.m2/repository/cn/dreamtobe/splitaar/sample/splitaar-sample-impl`

task apiJar(type: Jar) {
    baseName API_POM_ARTIFACT
    from android.sourceSets.main.java.srcDirs
    exclude '**/impl/**'
}

task implSourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        impl(MavenPublication) {
            groupId GROUP
            artifactId IMPL_POM_ARTIFACT
            version VERSION_NAME

            artifact(implSourceJar)
            artifact("$buildDir/outputs/aar/library-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    if (it.group != null && (it.name != null || "unspecified".equals(it.name)) && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
        api(MavenPublication) {
            groupId GROUP
            artifactId API_POM_ARTIFACT
            version VERSION_NAME

            artifact apiJar
            // If your api dependent any libs please implement pom.withXml in here.
        }
    }

    repositories {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def mvnName = properties.getProperty("mvn.dev.user")
        def mvnPassword = properties.getProperty("mvn.dev.password")

        maven {
            // you even can publish to the repo folder in the buildDirwith : url "$buildDir/repo"
            url "${REPOSITORY_URL}${VERSION_NAME.endsWith('-SNAPSHOT') ? '-snapshot' : ''}"
            credentials {
                username mvnName
                password mvnPassword
            }
        }
    }
}